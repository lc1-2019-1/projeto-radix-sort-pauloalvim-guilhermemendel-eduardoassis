Provas do radix_sort.pvs

- d_nth(n,d) -> Retorna o dth digito de n.

-n_digits(n) -> retorna o numero de digitos de um natural

- d_digits_gt(n) -> prova que 10^(numero de digitos de n) Ã© maior que n 

% Three simple properties of remainder and exponential

-rem_div_div(n,a,b) -> prova que n/(a*b) = (n/a)/b

-rem_b_j_rem_div_times_b(n,d,b) -> 
   FORALL(n, d : nat, b: posnat) :
      rem(b ^ d)((n - rem(b)(n))/b)  * b = rem(b ^ (d + 1))(n - rem(b)(n))

 rem_b_j_rem_b  : LEMMA
   FORALL(n : nat, d, b: posnat) :
      rem(b ^ d)(n) = rem(b ^ d)(n - rem(b)(n))  + rem(b)(n)

% This uses previous two properties and states that the (d)th digit times 10^d
% plus the d less significative digits is equal to the (d+1) less signigicative
% digits of a natural. 
 d_exp_dec  :LEMMA
  FORALL(n, d : nat) : d_nth(n, d) * 10 ^ d + rem(10 ^ d)(n) =
       rem(10 ^ (1 + d))(n)


 max_digits(l : list[nat] | length(l) >= 1)  : RECURSIVE posnat =
   IF length(l) = 1 THEN n_digits(car(l))
   ELSE max(n_digits(car(l)), max_digits(cdr(l)))
   ENDIF
   MEASURE length(l)

% is_sorted_ud? holds for lists of naturals sorted until the dth digit in their
% decimal representation.
 is_sorted_ud?(l : list[nat], d : nat) : bool = FORALL(i : below[length(l)], j : below[length(l)] | i <= j) :
    rem(10^d)(nth(l,i)) <= rem(10^d)(nth(l,j))

% Merges lists of naturals non decreasingly by the dth digit in their
% decimal representation
merge(l1, l2 : list[nat], d : nat) : RECURSIVE list[nat] =
IF null?(l1) OR null?(l2) THEN append(l1, l2)
ELSIF d_nth(car(l1),d) <= d_nth(car(l2),d) THEN cons(car(l1), merge(cdr(l1),l2, d))
ELSE cons(car(l2), merge(l1, cdr(l2), d))
ENDIF
MEASURE length(l1) + length(l2)

% Merge sort algorithm by the dth digit
merge_sort(l : list[nat], d :nat) : RECURSIVE list[nat] =
 IF length(l) <= 1 THEN l 
 ELSE merge(merge_sort(prefix(l,floor(length(l) / 2)), d), 
                 merge_sort(suffix(l,floor(length(l)/2)), d), d)
 ENDIF 
MEASURE length(l)

% Radixsort recursively sorts a list of naturals by its digist dth
% until kth using merge_sort.
 radixsort(l : list[nat], k : nat, d : nat | d <= k) : RECURSIVE list[nat] =
     IF d = k THEN merge_sort(l,d)
     ELSE radixsort(merge_sort(l,d), k, d+1)
     ENDIF
     MEASURE k - d

% radixsort is the main function, that calls the previous ones to sort
% a list of naturals starting from the 0th until the maximum
% number of digits in the representations of the naturals in the list.
% The last is computed using function max_digits. 
 radixsort(l : list[nat]) : list[nat] =
  IF length(l) <= 1 THEN l
  ELSE radixsort(l, max_digits(l), 0)
  ENDIF

% sort_digit? holds when l is non decreasingly sorted by digit dth.
  sort_digit?(l: list[nat], d : nat) :bool =
  FORALL(i,j : below(length(l))) :
  i <= j => d_nth(nth(l,i),d) <= d_nth(nth(l,j),d)

% merge of list preserves length
  merge_length : LEMMA
  FORALL(l1, l2 : list[nat], d : nat):
  length(merge(l1,l2,d)) = length(l1) + length(l2)

% merge_sort preserves length
  merge_sort_length : LEMMA
  FORALL(l : list[nat], d : nat) :
  length(l) = length(merge_sort(l,d))

% Lemma states that merge permutes (preserves) data
  merge_permutes : LEMMA
  FORALL(l1, l2 : list[nat], d : nat):
  permutations(append(l1,l2),merge(l1,l2,d))

% Lemma states that merge_sort permutes (preserves) data.
  merge_sort_permutes : LEMMA
  FORALL(l : list[nat], d : nat):
  permutations(l,merge_sort(l,d))

% Lemma states that two lists sorted until the (d+1)th digit
% that consist of sets of naturals smaller than or equal
% and greater than or equal the other set, respctively,
% and that are merged by their dth digit will give a
% list that is sorted until the (d+1)th digit. 
  merge_preserves_sort : LEMMA
  FORALL(l1,l2 : list[nat], d : nat):
  (is_sorted_ud?(l1,d+1) AND is_sorted_ud?(l2,d+1) AND
   FORALL(i:below[length(l1)], j:below[length(l2)]) :
    rem(10^d)(nth(l1,i))  <= rem(10^d)(nth(l2,j)) )  =>
   is_sorted_ud?(merge(l1,l2,d),d+1)


% lists sorted until the dth digit that are merge sorted
% by its dth digit will be sorted until ith (d+1)th digit.
  merge_sort_d_sorts : LEMMA
   FORALL(l : list[nat], d : nat) :
    is_sorted_ud?(l,d)  => 
    is_sorted_ud?(merge_sort(l,d),d+1)



%************************************************************
%*** QUESTION 01
%%   Apply strong induction in "k-d" using
%%   command '(measure-induct+ "k-d" ("k" "d"))'
%%   You will also need apply previous lemma. 
%%   
%************************************************************
% A list of naturals sorted until their dth digit that are radix sorted
% from their dth until their kth digits are sorted until their
% (k+1)th digit. 
  radix_sort_d_sort   : CONJECTURE 
   FORALL(l : list[nat], k : nat, d:nat | d <= k) :
     is_sorted_ud?(l,d) =>
        is_sorted_ud?(radixsort(l, k, d), k+1) 

%**************************************************************
%*** QUESTION 02
%*** Apply strong induction in "k-d" using
%*** command '(measure-induct+ "k-d" ("k" "d"))'
%*** You will need also apply lemmas on properties of 
%*** merge_sort and transitivity of permutations.
%**************************************************************
% radixsort from the dth until the kth digit permutes (preserves) data.
 radixsort_permutes : CONJECTURE
  FORALL(l : list[nat], k : nat, d : nat | d <=k):
  permutations(l, radixsort(l,k,d))

% max_digits of a list of naturals is enough to consider all numbers 
% in the list by all their digits. 
  rem_max_digits : LEMMA
  FORALL(l : list[nat] | length(l) >= 1, i : below[length(l)]):
  rem(10^max_digits(l))(nth(l,i)) = nth(l,i)

% radixsort permutes (preserves) data.
  radixsort_permutes2 : LEMMA
  FORALL(l : list[nat]):
  permutations(l,radixsort(l))


%************************************************************
%**  QUESTION 03
%** Apply previous lemmas.
%** And, perhaps some lemmas on properties of exponential
%** such as '(lemma "both_sides_expt_gt1_gt_aux")', and on
%** properties of remainder as  '(lemma "rem_mod2")'
%************************************************************

% radixsort sorts lists of naturals
  radixsort_sorts : CONJECTURE
  FORALL(l : list[nat]):
   is_sorted?(radixsort(l))

% Main result on correction of radixsort 
radixsort_works  : THEOREM
  FORALL(l : list[nat]):
    permutations(l,radixsort(l)) AND is_sorted?(radixsort(l))
    
END radix_sort
